plugins {
    id 'java-library'
    //Remove 'com.jfrog.artifactory' plugin if you are not using Artifactory
    id 'com.jfrog.artifactory' version '4.9.5'
    id 'maven-publish'

}

group 'one.block'

repositories {
    //Remove maven{} block if you are not using Artifactory 
    maven {
        credentials{
            username artifactory_username
            password artifactory_password
        }
        url artifactory_path_android_libraries
        
    }

    //Uncomment if you are not using Artifactory
    /*
    jcenter()
    mavenCentral()
     */
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'one.block:eosiojava:0.1.2-cfd'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.mockito:mockito-core:3.0.0'
    testCompile 'org.powermock:powermock-module-junit4:2.0.2'
    testCompile 'org.powermock:powermock-api-mockito2:2.0.2'
}

//Changes module cache control ---- Default is 24 hrs
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
}

def libraryGroupId = 'one.block'
def libraryArtifactId = 'eosiojavasoftkeysignatureprovider'
def libraryVersion = '0.1.3-cfd'

task sourcesJar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        jar(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/libs/${artifactId}.jar")
        }
    }
}

//Remove artifactory{} block if you are not using Artifactory
artifactory {
    contextUrl = artifactory_contextURL
    publish {
        repository {
            repoKey = artifactory_repo

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('jar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
